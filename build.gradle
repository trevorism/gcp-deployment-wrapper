buildscript {
    ext {
        creds = {
            username = findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://maven.pkg.github.com/trevorism/gradle-acceptance-plugin")
            credentials creds
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.8.1'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.7.2'
    }
}

plugins {
    id("groovy")
}

import com.trevorism.DeployPlugin
apply plugin: DeployPlugin

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
}

project(':service') {
    apply plugin: 'com.google.cloud.tools.appengine'
    apply plugin: "com.trevorism.gradle.acceptance"

    group = "com.trevorism"

    dependencies {
        testImplementation 'com.trevorism:test-result-events:0.3.0'
    }

    appengine {
        stage.artifact = layout.buildDirectory.file("libs/${project.name}-all.jar")
        deploy {
            projectId = "trevorism-gcloud"
            version = "0-6-0"
            stopPreviousVersion = true
            promote = true
        }
    }

    test {
        systemProperty("trevorism.test.event", findProperty("trevorism.test.event") == "enabled" ? project.name : null)
    }

}